dnl Process this file with autoconf to produce a configure script.
AC_INIT(JSBSim.cpp, 1.0, jsb@hal-pc.org)

dnl set the $host variable based on local machine/os
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE(JSBSim, 1.0)

AC_ARG_ENABLE([libraries],
    [AS_HELP_STRING([--enable-libraries],
        [build JSBSim libraries [default=no]])],
     [build_libraries=$enableval],
     [build_libraries=no])
AM_CONDITIONAL(BUILD_LIBRARIES, test "x$build_libraries" = xyes)

dnl Checks for programs.
AC_DISABLE_SHARED
AC_PROG_CPP
AC_PROG_AWK
AC_PROG_CC
AC_PROG_LIBTOOL
AC_PROG_CXX

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl Checks for library functions.
AC_FUNC_MEMCMP

case "${host}" in
*-*-irix*)
    if test "$CXX" = "CC"; then
        AR="CC -ar"
        ARFLAGS="-o"
        CXXFLAGS="$CXXFLAGS -I$(top_srcdir)/include/mipspro -LANG:libc_in_namespace_std=off"
    else
        AR="ar"
        ARFLAGS="cru"
    fi
    ;;
*-*-mingw32*)
    LIBS="$LIBS -lwsock32"
    ;;
*)
    if test "$CXX" = "g++"; then
       CXXFLAGS="$CXXFLAGS -Wno-non-template-friend"
    fi
    AR="ar"
    ARFLAGS="cru"
    ;;
esac
AC_SUBST(AR)
AC_SUBST(ARFLAGS)

dnl check for socklen_t (in Unix98)
AC_MSG_CHECKING(for socklen_t)
AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/socket.h>
socklen_t apa;
],[],[
AC_MSG_RESULT(yes)],[
AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/socket.h>
int accept (int, struct sockaddr *, size_t *);
],[],[
AC_MSG_RESULT(size_t)
AC_DEFINE(socklen_t,size_t)], [
AC_MSG_RESULT(int)
AC_DEFINE(socklen_t,int)])])

AC_OUTPUT( \
Makefile \
src/Makefile \
src/initialization/Makefile \
src/models/Makefile \
src/models/atmosphere/Makefile \
src/models/propulsion/Makefile \
src/models/flight_control/Makefile \
src/input_output/Makefile \
src/math/Makefile \
src/simgear/Makefile \
src/simgear/props/Makefile \
src/simgear/xml/Makefile \
src/utilities/Makefile \
src/utilities/aeromatic/Makefile \
aircraft/Makefile \
aircraft/737/Makefile \
aircraft/A320/Makefile \
aircraft/B747/Makefile \
aircraft/OV10/Makefile \
aircraft/SGS/Makefile \
aircraft/Submarine_Scout/Makefile \
aircraft/T37/Makefile \
aircraft/T38/Makefile \
aircraft/X15/Makefile \
aircraft/ball/Makefile \
aircraft/c172p/Makefile \
aircraft/c172r/Makefile \
aircraft/c172x/Makefile \
aircraft/c182/Makefile \
aircraft/c310/Makefile \
aircraft/f104/Makefile \
aircraft/f15/Makefile \
aircraft/f16/Makefile \
aircraft/f22/Makefile \
aircraft/fokker100/Makefile \
aircraft/fokker50/Makefile \
aircraft/minisgs/Makefile \
aircraft/mk82/Makefile \
aircraft/p51d/Makefile \
aircraft/pa28/Makefile \
aircraft/paraglider/Makefile \
aircraft/sgs126/Makefile \
aircraft/shuttle/Makefile \
aircraft/t6texan2/Makefile \
aircraft/x24b/Makefile \
engine/Makefile \
systems/Makefile \
scripts/Makefile \
)
